Program ::= ProgramHead DeclarePart ProgramBody DOT
ProgramHead ::= PROGRAM ProgramName
ProgramName ::= ID
DeclarePart ::= TypeDecpart VarDecpart ProcDecpart
TypeDecpart ::= $ | TypeDec
TypeDec ::= TYPE TypeDecList
TypeDecList ::= TypeId EQ TypeDef SEMI TypeDecMore
TypeDecMore ::= $ | TypeDecList
TypeId ::= ID
TypeDef ::= BaseType | StructureType | ID
BaseType ::= INTEGER | CHAR
StructureType ::= ArrayType | RecType
ArrayType ::= ARRAY LMIDPAREN Low UNDERRANGE Top RMIDPAREN OF BaseType
Low ::= INTC
Top ::= INTC
RecType ::= RECORD FieldDecList END
FieldDecList ::= BaseType IdList SEMI FieldDecMore | ArrayType IdList SEMI FieldDecMore
FieldDecMore ::= $ | FieldDecList
IdList ::= ID IdMore
IdMore ::= $ | COMMA IdList
VarDecpart ::= $ | VarDec
VarDec ::= VAR VarDecList
VarDecList ::= TypeDef VarIdList SEMI VarDecMore
VarDecMore ::= $ | VarDecList
VarIdList ::= ID VarIdMore
VarIdMore ::= $ | COMMA VarIdList
ProcDecpart ::= $ | ProcDec
ProcDec ::= PROCEDURE ProcName LPAREN ParamList RPAREN SEMI inProcDecpart ProcBody ProcDecMore
ProcDecMore ::= $ | ProcDec
ProcName ::= ID
ParamList ::= $ | ParamDecList
ParamDecList ::= Param ParamMore
ParamMore ::= $ | SEMI ParamDecList
Param ::= TypeDef FormList | VAR TypeDef FormList
FormList ::= ID FidMore
FidMore ::= $ | COMMA FormList
inProcDecpart ::= DeclarePart
ProcBody ::= ProgramBody
ProgramBody ::= BEGIN StmList END
StmList ::= Stm StmMore
StmMore ::= $ | SEMI StmList
Stm ::= ConditionalStm | LoopStm | InputStm | OutputStm | ReturnStm | ID AssCall
AssCall ::= AssignmentRest | CallStmRest
AssignmentRest ::= VariMore ASSIGN Exp
ConditionalStm ::= IF RelExp THEN StmList ELSE StmList FI
LoopStm ::= WHILE RelExp DO StmList ENDWH
InputStm ::= READ LPAREN Invar RPAREN
Invar ::= ID
OutputStm ::= WRITE LPAREN Exp RPAREN
ReturnStm ::= RETURN LPAREN Exp RPAREN
CallStmRest ::= LPAREN ActParamList RPAREN
ActParamList ::= $ | Exp ActParamMore
ActParamMore ::= $ | COMMA ActParamList
RelExp ::= Exp OtherRelE
OtherRelE ::= CmpOp Exp
Exp ::= Term OtherTerm
OtherTerm ::= $ | AddOp Exp
Term ::= Factor OtherFactor
OtherFactor ::= $ | MultOp Term
Factor ::= LPAREN Exp RPAREN | INTC | Variable
Variable ::= ID VariMore
VariMore ::= $ | LMIDPAREN Exp RMIDPAREN | DOT FieldVar
FieldVar ::= ID FieldVarMore
FieldVarMore ::= $ | LMIDPAREN Exp RMIDPAREN
CmpOp ::= LT | EQ
AddOp ::= PLUS | MINUS
MultOp ::= TIMES | DIVIDE